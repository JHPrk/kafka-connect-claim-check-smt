plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

group = 'com.github.cokelee777'
version = '0.0.1'

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
}

ext {
    kafkaVersion = '3.8.1'
    awsSdkVersion = '2.41.3'
    jedisVersion = '7.2.0'

    junitVersion = '5.11.0'
    mockitoVersion = '5.21.0'
    testcontainersVersion = '1.21.4'
    slf4jVersion = '2.0.17'
}

dependencies {
    compileOnly "org.apache.kafka:connect-api:${kafkaVersion}"
    compileOnly "org.apache.kafka:connect-transforms:${kafkaVersion}"
    compileOnly "org.apache.kafka:connect-json:${kafkaVersion}"

    implementation "software.amazon.awssdk:s3:${awsSdkVersion}"
    implementation "redis.clients:jedis:${jedisVersion}"

    compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"

    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:localstack:${testcontainersVersion}"

    testImplementation "org.apache.kafka:connect-api:${kafkaVersion}"
    testImplementation "org.apache.kafka:connect-transforms:${kafkaVersion}"
    testImplementation "org.apache.kafka:connect-json:${kafkaVersion}"
    testImplementation "com.redis.testcontainers:testcontainers-redis-junit:1.6.4"
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"
}

test {
    useJUnitPlatform()
    // 테스트 시 불필요한 경고 숨기기 (Java 17+ 이슈 대응)
    jvmArgs '-XX:+EnableDynamicAgentLoading'
}

shadowJar {
    archiveClassifier.set('')
    destinationDirectory = file("${buildDir}/libs/kafka-connect-smt-toolkit")

    dependencies {
        exclude(dependency("org.apache.kafka:.*"))
        exclude(dependency("org.slf4j:.*"))
    }

    // 충돌 방지를 위한 패키지 재배치 (Relocation) - 선택 사항이지만 권장
    // 예: 내가 쓰는 Jedis 버전과 Kafka Connect가 내부적으로 쓰는 Jedis가 다를 경우를 대비
    relocate 'redis.clients', 'com.github.cokelee777.shadow.redis.clients'
    relocate 'software.amazon.awssdk', 'com.github.cokelee777.shadow.awssdk'

    mergeServiceFiles()
}

publishing {
    publications {
        gpr(MavenPublication) {
            artifact shadowJar

            groupId = group
            artifactId = 'kafka-connect-smt-toolkit'
            version = version
        }
    }

    // 3. 배포할 저장소 위치 (GitHub Packages)
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cokelee777/kafka-connect-smt-toolkit")

            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}