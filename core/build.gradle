plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0'
    id 'maven-publish'
}

dependencies {
    // Kafka Connect
    compileOnly "org.apache.kafka:connect-api:${kafkaVersion}"
    compileOnly "org.apache.kafka:connect-transforms:${kafkaVersion}"
    compileOnly "org.apache.kafka:connect-json:${kafkaVersion}"

    // Log
    compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"

    // AWS SDK
    implementation platform("software.amazon.awssdk:bom:${awsSdkVersion}")
    implementation "software.amazon.awssdk:s3"
    implementation "software.amazon.awssdk:apache-client"

    // Kafka Connect(Test)
    testImplementation "org.apache.kafka:connect-api:${kafkaVersion}"
    testImplementation "org.apache.kafka:connect-transforms:${kafkaVersion}"
    testImplementation "org.apache.kafka:connect-json:${kafkaVersion}"

    // Log(Test)
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"

    // JUnit
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    // AssertJ
    testImplementation "org.assertj:assertj-core:${assertjVersion}"

    // Mockito
    testImplementation platform("org.mockito:mockito-bom:${mockitoVersion}")
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

shadowJar {
    doFirst {
        def destDir = destinationDirectory.get().asFile
        if (destDir.exists()) {
            delete destDir
        }
        destDir.mkdirs()
    }

    archiveBaseName.set("kafka-connect-claim-check-smt")
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('')
    destinationDirectory.set(layout.buildDirectory.dir("libs/kafka-connect-claim-check-smt").get().asFile)

    dependencies {
        exclude(dependency("org.apache.kafka:.*"))
        exclude(dependency("org.slf4j:.*"))
    }

    // Relocation packages from conflict
    relocate 'software.amazon.awssdk', 'com.github.cokelee777.shadow.awssdk'
    mergeServiceFiles()
}

publishing {
    publications {
        gpr(MavenPublication) {
            artifact shadowJar

            groupId = group
            artifactId = 'kafka-connect-claim-check-smt'
            version = version

            pom {
                name = 'kafka-connect-claim-check-smt'
                description = 'Kafka Connect SMT for implementing Claim Check pattern with multiple storage support'
                url = 'https://github.com/cokelee777/kafka-connect-claim-check-smt'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'cokelee777'
                        name = 'cokelee777'
                        email = 'akak640@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cokelee777/kafka-connect-claim-check-smt.git'
                    developerConnection = 'scm:git:ssh://github.com/cokelee777/kafka-connect-claim-check-smt.git'
                    url = 'https://github.com/cokelee777/kafka-connect-claim-check-smt'
                }
            }
        }
    }

    // GitHub Packages
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cokelee777/kafka-connect-claim-check-smt")

            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}